TITLE:: ProxyChainPreset
summary:: a preset class for ProxyChain
categories:: JITLib
related:: Classes/ProxyChain, Classes/MainFX

DESCRIPTION::

ProxyChainPreset can store presets for ProxyChain objects:
A setting includes which functions are active (by name),
and separate parameter settings for each active function.


First code examples:

code::

s.boot;

// first, prepare for making a proxy chain:

(
// some sound functions - first a test source,
(
// add a sound source
ProxyChain.add3(
	\dust,
	\mix -> { |dens=20, dustdec=0.02, dustfreq= 600|
		Ringz.ar(Dust2.ar(dens).lag(0.0001), dustfreq, dustdec)
	},
	0.2,
	[
		\dens, [0.1, 1000, \exp],
		\dustdec, [0.0001, 0.1, \exp],
		\dustfreq, \freq
	]
);

// ... and then some effects to use in the chain
ProxyChain.add3(
	\ringmod,
	\filter -> { |in, randrate=5|
		in.asArray[0]	 // force mono inputs
		* SinOsc.ar(LFNoise0.kr([randrate, randrate]).exprange(300, 3000)).sum
	},
	0.5, // wet/dry mix of 50/50
	[ \randrate, [0.5, 50, \exp] ]
);

ProxyChain.add3(
	\dist, \filter -> { |in, drive=10, amp=0.2| (in.asArray[0] * drive).tanh * amp },
	1, // all wet, just the dito
	[ \drive, [1, 100, \exp] ],
);

ProxyChain.add3(
	\comb5, \filterIn -> { |in, drive=10, amp=0.2| (in.asArray[0] * drive).clip2(0.5) * amp },
	0.2,
	[ \drive, [1, 100, \exp] ],
);


ProxyChain.add(
	\ampFin,
	\filter -> { |in, drive=1, limit=1| Limiter.ar(in * drive, limit) },
	1,
	[
		\drive, [1, 100, \exp],
		\limit, [0.1, 1.0, \exp]
	]
);
);

// now make a proxy chain and gui for it
c = ProxyChain(\alpha, [\dust, \ringmod, \dist, \ampFin]);
c.play;	// play the proxy
g = c.gui(12).moveTo(0, 800);


// 2. make a ProxyChainPreset for c
b = ProxyChainPreset(c);
// declare ampFin as always untouched / not to be stored
b.exceptedSlots = [\ampFin];
)

b.slotsToStore; // these slots and their values will be stored when active
c.activeSlotNames; // these are currently active


c.add(\dust, 0.5);
c.add(\ringmod, 1);
c.activeSlotNames; // these are currently active
b.addSet(\test1);
b.settings.list;

// repeat unil you find one you like
c.proxy.setUni( *c.proxy.controlKeys.collect ( [_, 1.0.rand] ).flat );
b.addSet(\test2);
b.settings.prettyList; // two entries

// add a slot and add a setting
c.add(\dist, 1);
b.addSet(\test3);

// remove another and addSet
c.remove(\ringmod);
b.addSet(\test4);

c.remove(\dist);
c.proxy.set(\dustfreq, 3000, \dens, 7, \dustdec, 0.01);
b.addSet(\test5);

// now you have 5 different settings - switch between them:
b.settings.prettyList;
b.settings.names
b.setCurr(\test1);
b.setCurr(\test2);
b.setCurr(\test3);
b.setCurr(\test4);
b.setCurr(\test5);

// now write them to disk
b.settings.write;

b.settingNames;

::

CLASSMETHODS::

METHOD:: all
dict where all ProxyChainPreset objects are stored by name

private:: initClass

METHOD:: new
make a ProxyChainPreset for a key or a ProxyChain
argument:: keyOrObj

INSTANCEMETHODS::

private:: init, storeArgs, printOn

METHOD:: chain
the proxychain

METHOD:: proxy
the proxy of the proxychain

METHOD:: key
the key by which this ProxyChainPreset is stored.

METHOD:: slotsToStore
which slot names will be stored if active

METHOD:: exceptedSlots
get and set which slots are not to be stored

METHOD:: settings
the settings object that does storage

METHOD:: addSet, removeSet
add and remove a setting by name

METHOD:: getCurr
get current setting from proxy/chain

METHOD:: setCurr
set current setting
argument:: setName
name of setting
argument:: absolute
if true turns off slots not present in setting
argument:: except

METHOD:: writeSettings
write settings to disk

METHOD:: loadSettings
read settings from disk

METHOD:: settingIndex
argument:: setName

METHOD:: settingNames
get names of all settings


EXAMPLES::

Here is a quick and dirty gui sketch for a ProxyChainPresetGui,
assuming you still have b and c preset from above around:

code::
// assumes ProxyChainPreset b is still around
// writing alpha to disk will store here:
b.settings.storePath;
b.settings.filename;
// set the dirpath to something else:
b.settings.dirpath;

(
	w = Window("PCPre", Rect(0,600,500,500)).front;
	w.addFlowLayout;
	e = EZPopUpMenu(w, Rect(0,0,150, 20), \pcPre, b.settingNames,
		{ |ez| b.setCurr(ez.item) },
		labelWidth: 50);
	[
		\sto, { |but|
			b.settings.storeDialog('???',
				b.getCurr,
				but.absoluteBounds.center,
				// store settings to disk?
				{ b.settings.write }
			)
		},
		\del, 	{ |but|
			b.settings.deleteDialog(
				but.absoluteBounds.center,
				// store settings to disk?
				{ b.settings.write }
			)
		}
	].keysValuesDo { |key, fun|
		Button(w, Rect(0,0,60,20)).states_([[key]])
		.action_(fun)
	};
	g = ProxyChainGui(c, 20, w);

	// update e when stored names change:
	q.pcpSkip = q.pcpSkip ?? {
		SkipJack({
			var newnames = b.settings.names;
			if ( (newnames == q.pcpNames).not) {
				e.items = newnames;
			}
		}, 0.5, w.isClosed);
	};
)
::

An example of a MainFX / ProxyChainPreset combination,
using the same sound functions as above:
code::


m = MainFX(s, 2, [\dust, \ringmod, \dist, \ampFin]);
n = ProxyChainPreset(m.proxyChain);
n.exceptedSlots = [\ampFin];

// quick and dirty copy of the settings
n.settings.list.addAll(b.settings.list);

n.settings.names;

n.setCurr(n.settings.names.choose.postcs);
n.setCurr(\test2);
n.setCurr(\test3);

// and use the same gui as above for switching MainFX presets:
(
	w = Window("MainFX_s presets", Rect(0,600,500,500)).front;
	w.addFlowLayout;
	e = EZPopUpMenu(w, Rect(0,0,150, 20), \mfxPre, b.settingNames,
		{ |ez| n.setCurr(ez.item) },
		labelWidth: 50);
	[
		\sto, { |but| n.settings.storeDialog('???', b.getCurr, but.absoluteBounds.center.postcs) },
		\del, 	{ |but| n.settings.deleteDialog(but.absoluteBounds.center) }
	].keysValuesDo { |key, fun|
		Button(w, Rect(0,0,60,20)).states_([[key]])
		.action_(fun)
	};

	g = MainFXGui(m, 24, w);
)
::

